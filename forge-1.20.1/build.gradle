plugins {
    id 'com.gradleup.shadow'
}

architectury {
    platformSetupLoomIde()
    forge()
}

configurations {
    common
    shadowCommon
    compileClasspath.extendsFrom common
    runtimeClasspath.extendsFrom common
    developmentForge.extendsFrom common
}

sourceSets {
    main {
        java {
            srcDir '../forge-base/src/main/java'
            srcDir 'src/main/java'
        }
        resources {
            srcDirs = []
            // Version-specific resources FIRST (higher priority)
            srcDir 'src/main/resources'
            // Shared resources SECOND (lower priority)
            srcDir '../forge-base/src/main/resources'
        }
    }
}

def commonModule = ':common-1.20.1'

loom {
    accessWidenerPath = project(commonModule).loom.accessWidenerPath

    forge {
        mixinConfig "weatheredwells.mixins.json"
    }

    runs {
        client {
            client()
            setConfigName("Forge Client")
            ideConfigGenerated(true)
            runDir("run")
        }
        server {
            server()
            setConfigName("Forge Server")
            ideConfigGenerated(true)
            runDir("run")
        }
    }
}

dependencies {
    forge "net.minecraftforge:forge:${rootProject.ext.minecraft_version}-${rootProject.ext.forge_version}"
    modApi "dev.architectury:architectury-forge:${rootProject.ext.architectury_api_version}"

    common(project(path: commonModule, configuration: "namedElements")) { transitive false }
    shadowCommon(project(path: commonModule, configuration: "transformProductionForge")) { transitive false }
}

tasks.named("processResources", ProcessResources).configure {
    dependsOn project(commonModule).tasks.named('convertNbt')
    duplicatesStrategy = DuplicatesStrategy.EXCLUDE
    inputs.property "version", project.version

    filesMatching("META-INF/mods.toml") {
        expand "version": project.version
    }

    from(project(commonModule).sourceSets.main.resources) {
        duplicatesStrategy = DuplicatesStrategy.EXCLUDE
        exclude "fabric.mod.json"
        exclude "weatheredwells.mixins.json"
        exclude "architectury.common.json"
    }

    // Include converted NBT files from common module
    from(project(commonModule).layout.buildDirectory.dir('generated/nbt')) {
        duplicatesStrategy = DuplicatesStrategy.EXCLUDE
    }
}

tasks.named("shadowJar", com.github.jengelman.gradle.plugins.shadow.tasks.ShadowJar).configure {
    exclude "fabric.mod.json"
    exclude "architectury.common.json"

    configurations = [project.configurations.shadowCommon]
    archiveClassifier = "dev-shadow"
}

tasks.named("remapJar").configure {
    inputFile.set tasks.named("shadowJar").flatMap { it.archiveFile }
    dependsOn tasks.named("shadowJar")
    archiveBaseName = rootProject.archives_name
    archiveVersion = "${rootProject.mod_version}+${rootProject.ext.minecraft_version}-forge"
    archiveClassifier = null
}

tasks.named("jar").configure {
    archiveClassifier = "dev"
}

tasks.named("sourcesJar").configure {
    duplicatesStrategy = DuplicatesStrategy.EXCLUDE
    def commonSources = project(commonModule).tasks.named("sourcesJar")
    dependsOn commonSources
    from commonSources.flatMap { it.archiveFile }.map { zipTree(it) }
    archiveBaseName = rootProject.archives_name
    archiveVersion = "${rootProject.mod_version}+${rootProject.ext.minecraft_version}-forge"
}

components.java {
    withVariantsFromConfiguration(project.configurations.shadowRuntimeElements) {
        skip()
    }
}

publishing {
    publications {
        mavenForge(MavenPublication) {
            artifactId = "${archives_name}-forge"
            from components.java
        }
    }

    repositories {
    }
}
