plugins {
    id 'dev.architectury.loom'
    id 'com.gradleup.shadow'
}

architectury {
    platformSetupLoomIde()
    fabric()
}

configurations {
    common {
        canBeResolved = true
        canBeConsumed = false
    }
    compileClasspath.extendsFrom common
    runtimeClasspath.extendsFrom common
    developmentFabric.extendsFrom common

    shadowBundle {
        canBeResolved = true
        canBeConsumed = false
    }
}

def commonModule = ':common-1.21.5'

dependencies {
    modImplementation "net.fabricmc:fabric-loader:$rootProject.fabric_loader_version"
    modApi "net.fabricmc.fabric-api:fabric-api:$rootProject.fabric_api_version"
    modApi "dev.architectury:architectury-fabric:$rootProject.architectury_api_version"

    common(project(path: commonModule, configuration: 'namedElements')) { transitive false }
    shadowBundle project(path: commonModule, configuration: 'transformProductionFabric')
}

sourceSets {
    main {
        java {
            srcDir '../fabric-base/src/main/java'
            srcDir 'src/main/java'
        }
        resources {
            srcDir 'src/main/resources'
            srcDir '../fabric-base/src/main/resources'
        }
    }
}

loom {
    if (project(commonModule).loom.accessWidenerPath != null) {
        accessWidenerPath = project(commonModule).loom.accessWidenerPath
    }

    runs {
        client {
            client()
            runDir("run")
        }
        server {
            server()
            runDir("run")
        }
    }
}

processResources {
    from(project(commonModule).sourceSets.main.resources)
    duplicatesStrategy = DuplicatesStrategy.EXCLUDE

    inputs.property 'version', project.version

    filesMatching('fabric.mod.json') {
        expand 'version': project.version
    }
}

shadowJar {
    configurations = [project.configurations.shadowBundle]
    archiveClassifier = 'dev-shadow'
}

remapJar {
    inputFile.set shadowJar.archiveFile
    dependsOn shadowJar
    archiveBaseName = rootProject.archives_name
    archiveVersion = "${rootProject.mod_version}+${rootProject.ext.minecraft_version}-fabric"
    archiveClassifier = null
}

java {
    withSourcesJar()
}

tasks.named('sourcesJar').configure {
    archiveBaseName = rootProject.archives_name
    archiveVersion = "${rootProject.mod_version}+${rootProject.ext.minecraft_version}-fabric"
    duplicatesStrategy = DuplicatesStrategy.EXCLUDE
}

tasks.named('remapSourcesJar').configure {
    archiveBaseName = rootProject.archives_name
    archiveVersion = "${rootProject.mod_version}+${rootProject.ext.minecraft_version}-fabric"
}

publishing {
    publications {
        maven(MavenPublication) {
            artifactId = "${rootProject.archives_name}-fabric"
            from components.java
        }
    }

    repositories {
    }
}
